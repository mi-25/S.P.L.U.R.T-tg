/obj/item/clothing/neck/size_collar
	name = "尺寸项圈"
	desc = "一个闪亮的黑色项圈，内嵌了允许使用者改变自己尺寸的技术。"
	icon = 'modular_skyrat/modules/modular_items/lewd_items/icons/obj/lewd_clothing/lewd_neck.dmi'
	worn_icon = 'modular_skyrat/modules/modular_items/lewd_items/icons/mob/lewd_clothing/lewd_neck.dmi'
	icon_state = "collar_black"
	kink_collar = TRUE
	/// Have we given the user the warning message yet?
	var/warning_given = FALSE
	/// The `temporary_size` component we have attached to the wearer.
	var/datum/component/temporary_size/size_component
	/// What size do we want to set the wearer to when they wear the collar?
	var/target_size = 1

/obj/item/clothing/neck/size_collar/attack_self(mob/user, modifiers)
	. = ..()
	if(!warning_given)
		if(tgui_alert(user, "此物品严格用作ERP物品，仅限在宿舍房间内使用。不遵守此规定将导致管理措施。你希望继续使用此物品吗？", "警告。", list("是", "否")) != "是")
			return FALSE

		warning_given = TRUE

	var/chosen_size = tgui_input_number(user, "你希望将项圈设置为什么尺寸百分比？", name, 100, CONFIG_GET(number/size_collar_maximum), CONFIG_GET(number/size_collar_minimum))
	if(!chosen_size)
		balloon_alert(user, "无效的尺寸！")
		return FALSE

	log_message("[src] had its target size changed to [chosen_size]% by [usr]", LOG_ATTACK)
	balloon_alert(user, "设置为[chosen_size]%")
	target_size = (chosen_size * 0.01)
	return TRUE

/obj/item/clothing/neck/size_collar/mob_can_equip(mob/living/user, slot, disable_warning, bypass_equip_delay_self, ignore_equipped, indirect_action)
	if(!warning_given)
		return FALSE

	return ..()

/obj/item/clothing/neck/size_collar/equipped(mob/living/user, slot)
	. = ..()
	if(!ishuman(user) || !(slot & ITEM_SLOT_NECK))
		return FALSE

	size_component = user.AddComponent(/datum/component/temporary_size, target_size)
	size_component.target_size = target_size

	user.log_message("[src] was equipped by [user].", LOG_ATTACK)

/obj/item/clothing/neck/size_collar/dropped(mob/living/user)
	. = ..()
	if(size_component)
		qdel(size_component)
		size_component = null

/obj/item/clothing/neck/size_collar/examine(mob/user)
	. = ..()
	var/list/area_names = list()
	for(var/area_index in SIZE_WHITELISTED_AREAS) //We can't do this typed.
		var/area/area_type = area_index //So we have to assign it to a typed variable after we get it from the loop.
		var/area_name = initial(area_type.name)
		if(!area_name)
			continue

		area_names += area_name

	if(length(area_names))
		. += span_cyan("此项圈将在以下区域工作：[english_list(area_names)]")

	return .

/// Component that temporarily applies a size to a human.
/datum/component/temporary_size
	/// List containing the areas that the size change works in. If this is empty, this will work everywhere.
	var/list/allowed_areas = SIZE_WHITELISTED_AREAS
	/// What is the stored size of the mob using this?
	var/original_size = 1
	/// What size are we changing the parent mob to?
	var/target_size = 1

/datum/component/temporary_size/Initialize(size_to_apply)
	. = ..()
	if(!ishuman(parent))
		return COMPONENT_INCOMPATIBLE

	var/mob/living/carbon/human/human_parent = parent
	original_size = human_parent?.dna.features["body_size"]

	if(!original_size) //If we aren't able to get the original size, we shouldn't exist.
		return COMPONENT_INCOMPATIBLE

	RegisterSignal(parent, COMSIG_ENTER_AREA, .proc/check_area)

	target_size = size_to_apply
	check_area()

/// Checks if we need to revert our size when entering a different area.
/datum/component/temporary_size/proc/check_area()
	var/area/current_area = get_area(parent)
	if(!length(allowed_areas) || is_type_in_list(current_area, allowed_areas))
		apply_size(target_size)
		return TRUE

	apply_size(original_size)
	return FALSE

/// Adjusts the sprite size of the parent mob based off `size_to_apply`.
/datum/component/temporary_size/proc/apply_size(size_to_apply)
	var/mob/living/carbon/human/human_parent = parent
	if(!human_parent || !size_to_apply || (human_parent.dna.features["body_size"] == size_to_apply))
		return FALSE

	if(isteshari(human_parent) || isvoxprimalis(human_parent)) // We check if the human_parent is a Vox Primalis or Teshari & temporarily disable the bodysize restriction
		human_parent.dna.species.body_size_restricted = FALSE

	human_parent.dna.features["body_size"] = size_to_apply
	human_parent.maptext_height = 32 * human_parent.dna.features["body_size"]
	human_parent.dna.update_body_size()
	return TRUE

/datum/component/temporary_size/Destroy(force, silent)
	var/mob/living/carbon/human/human_parent = parent
	apply_size(original_size)

	if(isteshari(human_parent) || isvoxprimalis(human_parent)) // We reapply it on destroy if they were
		human_parent.dna.species.body_size_restricted = TRUE
	UnregisterSignal(parent, COMSIG_ENTER_AREA)

	return ..()

//Heister kit
/obj/item/clothing/suit/jacket/det_suit/noir/heister
	name = "装甲西装外套"
	desc = "一件专业的西装外套，感觉比普通外套重得多。内衬标签上写着\"基于纳米机器人的自修复凯夫拉编织物\"。"
	armor_type = /datum/armor/heister
	/// How many hits we can take before the armor breaks, PAYDAY style
	var/armor_stacks = 2

/datum/armor/heister
	melee = 35
	bullet = 30
	laser = 30
	energy = 40
	bomb = 25
	fire = 50
	acid = 50
	wound = 10

/obj/item/clothing/suit/jacket/det_suit/noir/heister/Initialize(mapload)
	. = ..()
	AddComponent(/datum/component/shielded/suit, max_charges = armor_stacks, recharge_start_delay = 8 SECONDS, charge_increment_delay = 1 SECONDS, \
	charge_recovery = armor_stacks, lose_multiple_charges = FALSE, starting_charges = armor_stacks, shield_icon_file = null, shield_icon = null)

/obj/item/clothing/suit/jacket/det_suit/noir/heister/equipped(mob/living/user, slot)
	. = ..()
	if(!(slot & ITEM_SLOT_OCLOTHING))
		return
	RegisterSignal(user, COMSIG_LIVING_CHECK_BLOCK, PROC_REF(armor_reaction))

/obj/item/clothing/suit/jacket/det_suit/noir/heister/proc/armor_reaction(mob/living/carbon/human/owner, atom/movable/hitby, damage = 0, attack_text = "攻击", attack_type = MELEE_ATTACK, armour_penetration = 0)
	if(SEND_SIGNAL(src, COMSIG_ITEM_HIT_REACT, owner, hitby, attack_text, 0, damage, attack_type) & COMPONENT_HIT_REACTION_BLOCK)
		return SUCCESSFUL_BLOCK
	return NONE

/obj/item/clothing/gloves/latex/nitrile/heister
	desc = "昂贵的高密度无菌手套，比乳胶手套更厚。完美隐藏指纹。"
	clothing_traits = null
	siemens_coefficient = 0

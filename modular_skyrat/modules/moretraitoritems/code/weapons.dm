#define CALIBRE_14MM "14mm"

/obj/item/gun/ballistic/revolver/ocelot
	name = "柯尔特和平使者左轮手枪"
	desc = "一把改装过的和平使者左轮手枪，使用.357弹药。威力比普通.357小，但弹跳次数更多。" // We need tension...conflict. The world today has become too soft. We're living in an age where true feelings are suppressed. So we're going to shake things up a bit. We'll create a world dripping with tension... ...a world filled with greed and suspicion, bravery and cowardice.
	// this could probably be made funnier by reducing its damage multiplier but also making it so that every fired bullet has the wacky ricochets
	// but that's a different plate of cookies for a different glass of milk
	icon_state = "c38_panther"
	accepted_magazine_type = /obj/item/ammo_box/magazine/internal/cylinder

/obj/item/ammo_casing/c357/peacemaker
	name = ".357和平使者子弹壳"
	desc = "一个.357和平使者子弹壳。"
	caliber = CALIBER_357
	projectile_type = /obj/projectile/bullet/c357/peacemaker

/obj/projectile/bullet/c357/peacemaker
	name = ".357和平使者子弹"
	damage = 25
	wound_bonus = 0
	ricochets_max = 6
	ricochet_chance = 200
	ricochet_auto_aim_angle = 50
	ricochet_auto_aim_range = 6
	ricochet_incidence_leeway = 80
	ricochet_decay_chance = 1

/datum/design/c357/peacemaker
	name = "快速装弹器(.357和平使者)"
	id = "a357PM"
	build_type = AUTOLATHE
	materials = list(
		/datum/material/iron = SHEET_MATERIAL_AMOUNT * 2,
	)
	build_path = /obj/item/ammo_box/speedloader/c357/peacemaker
	category = list(
		RND_CATEGORY_HACKED,
		RND_CATEGORY_WEAPONS + RND_SUBCATEGORY_WEAPONS_AMMO,
	)

/obj/item/ammo_box/speedloader/c357/peacemaker
	name = "快速装弹器(.357和平使者)"
	desc = "设计用于快速装填左轮手枪。"
	icon_state = "357"
	ammo_type = /obj/item/ammo_casing/c357/peacemaker
	max_ammo = 7
	multiple_sprites = AMMO_BOX_PER_BULLET
	item_flags = NO_MAT_REDEMPTION


/obj/item/clothing/head/hats/sus_bowler
	name = "奇怪的圆顶礼帽"
	desc = "一顶深黑色的圆顶礼帽。帽子内部有一个光滑的红色S标志，里面绣着更小的X徽记。仔细检查会发现帽檐感觉异常沉重..."
	icon_state = "bowler"
	force = 10
	throwforce = 45
	throw_speed = 5
	throw_range = 9
	w_class = WEIGHT_CLASS_SMALL
	armour_penetration = 30 //5 points less then a double esword!
	sharpness = SHARP_POINTY
	attack_verb_continuous = list("劈砍", "刺戳", "切割", "撕裂", "划伤", "扯裂", "切碎", "砍击")
	attack_verb_simple = list("劈砍", "刺戳", "切割", "撕裂", "划伤", "扯裂", "切碎", "砍击")

///obj/item/clothing/head/hats/sus_bowler/throw_impact(atom/hit_atom, datum/thrownthing/throwingdatum)
	//var/caught = hit_atom.hitby(src, FALSE, FALSE, throwingdatum=throwingdatum)
	//if(thrownby && !caught)
		//addtimer(CALLBACK(src, TYPE_PROC_REF(/atom/movable, throw_at), thrownby, throw_range+2, throw_speed, null, TRUE), 0.1 SECONDS)
	//else
		//return ..()

///obj/item/clothing/head/hats/sus_bowler/throw_at(atom/target, range, speed, mob/thrower, spin=1, diagonals_first = 0, datum/callback/callback, force, gentle = FALSE, quickstart = TRUE)
	//if(ishuman(thrower))
		//var/mob/living/carbon/human/I = thrower
		//I.throw_mode_off(THROW_MODE_TOGGLE) //so they can catch it on the return.
	//return ..()


/*
* Malorian Arms 3516 14MM
* If you have this, you're a badass.
*/

/obj/item/gun/ballistic/automatic/pistol/robohand
	name = "马洛里安军械3516型"
	desc = "马洛里安军械3516型是一款14毫米重型手枪，采用钛合金框架和独特的木质握把。定制的动力端口和 \
	直接集成网络互连意味着只有拥有机械臂和智能枪链接的人才能充分发挥这款手枪的功能。"
	icon = 'modular_skyrat/modules/moretraitoritems/icons/3516.dmi'
	icon_state = "3516"
	w_class = WEIGHT_CLASS_NORMAL
	accepted_magazine_type = /obj/item/ammo_box/magazine/m14mm
	can_suppress = FALSE
	fire_sound = 'modular_skyrat/modules/moretraitoritems/sound/fire2.ogg'
	load_sound = 'modular_skyrat/modules/moretraitoritems/sound/reload.ogg'
	load_empty_sound = 'modular_skyrat/modules/moretraitoritems/sound/reload.ogg'
	eject_sound = 'modular_skyrat/modules/moretraitoritems/sound/release.ogg'
	eject_empty_sound = 'modular_skyrat/modules/moretraitoritems/sound/release.ogg'
	vary_fire_sound = FALSE
	rack_sound = 'modular_skyrat/modules/moretraitoritems/sound/slide.ogg'
	fire_sound_volume = 100
	bolt_wording = "该死的滑套"
	reload_time = 0 //FAST AS FUCK BOIS!
	var/unrestricted = FALSE

/obj/item/gun/ballistic/automatic/pistol/robohand/unrestricted
	unrestricted = TRUE

/obj/item/gun/ballistic/automatic/pistol/robohand/ranged_interact_with_atom(atom/interacting_with, mob/living/user, list/modifiers)
	if(can_use(user) == ITEM_INTERACT_SUCCESS)
		return ..()
	return ITEM_INTERACT_BLOCKING

/obj/item/gun/ballistic/automatic/pistol/robohand/ranged_interact_with_atom_secondary(atom/interacting_with, mob/living/user, list/modifiers)
	if(can_use(user) == ITEM_INTERACT_SUCCESS)
		return ..()
	return ITEM_INTERACT_BLOCKING

/// Checks if we have the roboarm to use the robo gun. Well, unless it's unrestricted
/obj/item/gun/ballistic/automatic/pistol/robohand/proc/can_use(mob/living/carbon/human/user)
	if(unrestricted)
		return ITEM_INTERACT_SUCCESS
	if(!istype(user))
		return ITEM_INTERACT_BLOCKING
	var/obj/item/bodypart/selected_hand = user.get_active_hand()
	if(IS_ORGANIC_LIMB(selected_hand))
		to_chat(user, span_warning("你似乎不知道如何使用[src]，或许你需要查看说明书？"))
		return ITEM_INTERACT_BLOCKING
	return ITEM_INTERACT_SUCCESS

/obj/item/gun/ballistic/automatic/pistol/robohand/insert_magazine(mob/user, obj/item/ammo_box/magazine/inserted_mag, display_message)
	if(!istype(inserted_mag, accepted_magazine_type))
		to_chat(user, span_warning("[inserted_mag]似乎不适合装进[src]..."))
		return FALSE
	if(!user.transferItemToLoc(inserted_mag, src))
		to_chat(user, span_warning("你似乎无法将[src]从手中取出！"))
		return FALSE
	magazine = inserted_mag
	if(display_message)
		to_chat(user, span_notice("你将新的[magazine_wording]装入了[src]。"))
	playsound(src, load_empty_sound, load_sound_volume, load_sound_vary)
	if(bolt_type == BOLT_TYPE_OPEN && !bolt_locked)
		chamber_round(TRUE)
	drop_bolt(user)
	update_appearance()
	animate(src, 0.2 SECONDS, 1, transform = turn(matrix(), 120)) //Le johnny robohand woosh woosh twirl
	animate(time = 0.2 SECONDS, transform = turn(matrix(), 240))
	animate(time = 0.2 SECONDS, transform = null)
	return TRUE

/obj/item/gun/ballistic/automatic/pistol/robohand/eject_magazine(mob/user, display_message, obj/item/ammo_box/magazine/tac_load)
	if(bolt_type == BOLT_TYPE_OPEN)
		chambered = null
	if(magazine.ammo_count())
		playsound(src, eject_sound, eject_sound_volume, eject_sound_volume) //This is why we've copied this proc, it should play the eject sound when ejecting.
	else
		playsound(src, eject_empty_sound, eject_sound_volume, eject_sound_volume)
	magazine.forceMove(drop_location())
	var/obj/item/ammo_box/magazine/old_mag = magazine
	if(tac_load)
		if (insert_magazine(user, tac_load, FALSE))
			to_chat(user, span_notice("你对[src]执行了精英战术换弹。"))
		else
			to_chat(user, span_warning("你扔掉了旧的[magazine_wording]，但新的不合适。真尴尬。"))
			magazine = null
	else
		magazine = null
	user.put_in_hands(old_mag)
	old_mag.update_appearance()
	if(display_message)
		to_chat(user, span_notice("你从[src]中抽出了[magazine_wording]。"))
	update_appearance()
	animate(src, transform = turn(matrix(), 120), time = 0.2 SECONDS, loop = 1) //Le johnny robohand again
	animate(transform = turn(matrix(), 240), time = 0.2 SECONDS)
	animate(transform = null, time = 0.2 SECONDS)

//Magazine stuff
/obj/item/ammo_box/magazine/m14mm
	name = "手枪弹匣(14毫米)"
	icon = 'modular_skyrat/modules/moretraitoritems/icons/3516_mag.dmi'
	icon_state = "14mm"
	base_icon_state = "14mm"
	ammo_type = /obj/item/ammo_casing/c14mm
	caliber = CALIBRE_14MM
	max_ammo = 10
	multiple_sprites = AMMO_BOX_FULL_EMPTY

/obj/item/ammo_casing/c14mm
	name = "14毫米子弹壳"
	desc = "一个14毫米子弹壳。真牛逼。"
	caliber = CALIBRE_14MM
	projectile_type = /obj/projectile/bullet/c14mm

/obj/projectile/bullet/c14mm
	name = "14毫米子弹"
	damage = 60
	embed_type = /datum/embedding/c14mm
	dismemberment = 50
	pierces = 1
	projectile_piercing = PASSCLOSEDTURF|PASSGRILLE|PASSGLASS

/datum/embedding/c14mm
	embed_chance = 90
	fall_chance = 3
	jostle_chance = 4
	ignore_throwspeed_threshold = TRUE
	pain_stam_pct = 0.4
	pain_mult = 5
	jostle_pain_mult = 9
	rip_time = 10

//nullrod katana
/obj/item/katana/weak/curator //This has the same stats as the curator's claymore
	desc = "一把古老的武士刀。由...锻造？嗯，没有明确说明，但肯定是正品！而且非常锋利！"
	force = 15
	block_chance = 30
	armour_penetration = 5

#undef CALIBRE_14MM
